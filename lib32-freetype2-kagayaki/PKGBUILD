# Maintainer: Julien Humbert <julroy67@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=freetype2
pkgname=lib32-$_pkgbasename-kagayaki
pkgver=2.7.1
pkgrel=1
pkgdesc="TrueType font rendering library"
arch=(x86_64)
license=('GPL' 'MIT')
url="http://freetype.sourceforge.net"
groups=('kagayaki-bundle-multilib')
depends=('lib32-zlib' 'lib32-bzip2' 'lib32-libpng' 'lib32-harfbuzz' $_pkgbasename-kagayaki)
makedepends=(gcc-multilib)
conflicts=('lib32-freetype2' 'lib32-freetype2-infinality-ultimate')
provides=('lib32-freetype2' 'lib32-freetype2-infinality-ultimate')
source=(http://download.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.bz2
	0001-Enable-table-validation-modules.patch
	0002-Enable-subpixel-rendering.patch
	0003-Enable-infinality-subpixel-hinting.patch)
sha1sums=('4d08a9a6567c6332d58e9a5f9a7e9e3fbce66789'
          'b31882ef5e8447e761acee1c4a44c0630cd4d465'
          'b1494810ed3aca25cdd8e8cedf634e5adfe6c09e'
          '41d27140fd590945e22e012c9dce62de3d6f11e6')

prepare() {
	# Rename source dir to allow building the demos
	mv freetype-${pkgver} freetype2
	
	cd freetype2
	patch -Np1 -i ../0001-Enable-table-validation-modules.patch
	patch -Np1 -i ../0002-Enable-subpixel-rendering.patch
	patch -Np1 -i ../0003-Enable-infinality-subpixel-hinting.patch
}

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
	
	cd freetype2
	./configure \
		--prefix=/usr \
		--libdir=/usr/lib32 \
		--disable-static
	make
}

check() {
	cd freetype2
	make -k check
}

package() {
	cd freetype2
	make DESTDIR="${pkgdir}" install
	
	rm -rf "${pkgdir}"/usr/{include,share,bin}
}
