# Maintainer: Julien Humbert <julroy67@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=freetype2-kagayaki
pkgver=2.7
pkgrel=1
pkgdesc="TrueType font rendering library"
arch=(i686 x86_64)
license=('GPL' 'MIT')
groups=('kagayaki-bundle')
url="http://www.freetype.org"
depends=('zlib' 'bzip2' 'sh' 'xorg-xrdb' 'libpng' 'harfbuzz')
makedepends=('libx11')
optdepends=('libx11: Some demo programs')
provides=("freetype2=$pkgver" 'freetype2-infinality-ultimate' 'libfreetype.so')
conflicts=('freetype2' 'freetype2-infinality')
install=freetype2-kagayaki.install
backup=('etc/profile.d/freetype2.sh')
source=(http://download.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.bz2
        http://download.savannah.gnu.org/releases/freetype/freetype-doc-${pkgver}.tar.bz2
        http://download.savannah.gnu.org/releases/freetype/ft2demos-${pkgver}.tar.bz2
        0001-Enable-table-validation-modules.patch
        0002-Enable-subpixel-rendering.patch
        0003-Enable-infinality-subpixel-hinting.patch
        0005-freetype-2.5.2-more-demos.patch
        freetype2.sh
	xft-settings.sh)
sha1sums=('f251029d5ab542a96dd4531605fe577185077560'
          '22949e7c809d7d5931331ca54da30e4d195669ea'
          'b7f7633ca0b828ca319afdd09a81b42592685d9c'
          'b31882ef5e8447e761acee1c4a44c0630cd4d465'
          'b1494810ed3aca25cdd8e8cedf634e5adfe6c09e'
          '41d27140fd590945e22e012c9dce62de3d6f11e6'
          '72cfecbe738085eec475e012617661ad0cc9b76f'
          'bc6df1661c4c33e20f5ce30c2da8ad3c2083665f'
          '03297bc904f35e2bc74ebe27c10acbf3b1f53dc0')


prepare() {
	# Rename source dir to allow building the demos
	mv freetype-${pkgver} freetype2
	
	cd freetype2
	patch -Np1 -i ../0001-Enable-table-validation-modules.patch
	patch -Np1 -i ../0002-Enable-subpixel-rendering.patch
	patch -Np1 -i ../0003-Enable-infinality-subpixel-hinting.patch
	
	cd ../ft2demos-${pkgver}
	# enable more demos
	patch -Np1 -i ../0005-freetype-2.5.2-more-demos.patch
	
	# Suppress RPATH
	sed -i '/X11_LIB:%=-R%/d' graph/x11/rules.mk
}

build() {
	cd freetype2
	./configure \
		--prefix=/usr \
		--disable-static
	make
	
	# Build demos
	cd ../ft2demos-${pkgver}
	make
}

check() {
	cd freetype2
	make -k check
}

package() {
	cd freetype2
	make DESTDIR="${pkgdir}" install
	install -Dm644 ../freetype2.sh "${pkgdir}/etc/profile.d/freetype2.sh"
	
	# Package docs
	install -dm755 "${pkgdir}/usr/share/doc"
	cp -a docs "${pkgdir}/usr/share/doc/${pkgname}"
	
	# Package demos
	cd ../ft2demos-${pkgver}
	for _i in bin/{f,t}t*; do
		libtool --mode=install install $_i "$pkgdir/usr/bin/"
	done
	
	# freetype2 runtime settings
	install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
	install -m755 "${srcdir}/xft-settings.sh" \
		"${pkgdir}/etc/X11/xinit/xinitrc.d/xft-settings.sh"
}
