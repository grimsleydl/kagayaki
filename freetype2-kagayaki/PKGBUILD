# Maintainer: Julien Humbert <julroy67@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=freetype2-kagayaki
pkgname=('freetype2-kagayaki' 'freetype2-demos-kagayaki')
pkgver=2.7.1
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL' 'MIT')
url="http://www.freetype.org"
makedepends=('libx11' 'zlib' 'bzip2' 'sh' 'libpng' 'harfbuzz')
source=(http://download.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.bz2
        http://download.savannah.gnu.org/releases/freetype/freetype-doc-${pkgver}.tar.bz2
        http://download.savannah.gnu.org/releases/freetype/ft2demos-${pkgver}.tar.bz2
        0001-Enable-table-validation-modules.patch
        0002-Enable-subpixel-rendering.patch
        0003-Enable-infinality-subpixel-hinting.patch
        0005-freetype-2.5.2-more-demos.patch
        freetype2.sh
		xft-settings.sh)
sha256sums=('3a3bb2c4e15ffb433f2032f50a5b5a92558206822e22bfe8cbe339af4aa82f88'
            '8e09cf22d1c98006fe3af160b7c3b7c847e4a3743963d6d89314d350b859dfb0'
            'd3f8a0d5a3f0d58701133458a8c1d3f97f658869f3c904b1fda447ed3b290ecd'
            '6d273254fd925d284e5f66e3861eaef69a4393f34872398b2c93af0d5e15d34e'
            '9da762d69d85ca825a8e4578aaaeba28aa17b8d8a3bca29929c4786b3a82c104'
            'd043fa4ce22b6e6cfafa0d2e2898b1c5cd000ec66094a97ab57c9126934ba371'
            '36484db4b926ed026e7f32570573493b5a9793a129f08d54383a26d65a6af89b'
            'f7f8e09c44f7552c883846e9a6a1efc50377c4932234e74adc4a8ff750606467'
            'f3631c544ad11fae99c30f36f672dc3f5d248a9d21278ecf556d8286b04c985e')


prepare() {
	# Rename source dir to allow building the demos
	mv freetype-${pkgver} freetype2
	
	cd freetype2
	patch -Np1 -i ../0001-Enable-table-validation-modules.patch
	patch -Np1 -i ../0002-Enable-subpixel-rendering.patch
	patch -Np1 -i ../0003-Enable-infinality-subpixel-hinting.patch
	
	cd ../ft2demos-${pkgver}
	# enable more demos
	patch -Np1 -i ../0005-freetype-2.5.2-more-demos.patch
	
	# Suppress RPATH
	sed -i '/X11_LIB:%=-R%/d' graph/x11/rules.mk
}

build() {
	cd freetype2
	./configure \
		--prefix=/usr \
		--disable-static
	make
	
	# Build demos
	cd ../ft2demos-${pkgver}
	make
}

check() {
	cd freetype2
	make -k check
}

package_freetype2-kagayaki() {
	pkgdesc="TrueType font rendering library"
	depends=('zlib' 'bzip2' 'sh' 'libpng' 'harfbuzz')
	provides=("freetype2=$pkgver" 'freetype2-infinality-ultimate' 'libfreetype.so')
	conflicts=('freetype2' 'freetype2-infinality-ultimate')
	replaces=('freetype2' 'freetype2-infinality-ultimate')
	groups=('kagayaki-bundle')
	install=freetype2-kagayaki.install
	backup=('etc/profile.d/freetype2.sh')

	cd freetype2
	make DESTDIR="${pkgdir}" install
	install -Dm644 ../freetype2.sh "${pkgdir}/etc/profile.d/freetype2.sh"

	# Package docs
	install -dm755 "${pkgdir}/usr/share/doc"
	cp -a docs "${pkgdir}/usr/share/doc/${pkgname}"

	cd ../ft2demos-${pkgver}
	mkdir -p $srcdir/ft-demos
	for _i in bin/{f,t}t*; do
		libtool --mode=install install $_i "$srcdir/ft-demos"
	done

	# freetype2 runtime settings
	install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
	install -m755 "${srcdir}/xft-settings.sh" \
		"${pkgdir}/etc/X11/xinit/xinitrc.d/xft-settings.sh"
}

package_freetype2-demos-kagayaki() {
	pkgdesc="Freetype tools and demos"
	depends=('freetype2' 'libx11')

	install -dm755 "${pkgdir}/usr/bin"
	cp -a $srcdir/ft-demos/* "${pkgdir}/usr/bin"
}
