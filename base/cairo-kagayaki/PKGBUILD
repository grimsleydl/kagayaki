# Maintainer: Julien Humbert <julroy67@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>

_basename=cairo
pkgname=cairo-kagayaki
pkgver=1.14.8
pkgrel=1
pkgdesc="Cairo vector graphics library"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
# requires libGL + libEGL - all libgl variants (mesa, nvidia-xxx-libgl/nvidia-utils) provide libEGL
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman' 'glib2' 'libgl' 'lzo')
makedepends=('libgl' 'librsvg' 'gtk2' 'poppler-glib' 'libspectre' 'gtk-doc' 'valgrind')
groups=('kagayaki-bundle')
provides=("cairo=$pkgver" 'cairo-xcb' 'cairo-infinality-ultimate')
replaces=('cairo-xcb' 'cairo-infinality-ultimate')
conflicts=('cairo' 'cairo-infinality-ultimate')
source=(https://cairographics.org/releases/$_basename-$pkgver.tar.xz
        cairo-make-lcdfilter-default.patch
        cairo-respect-fontconfig_pb.patch
        cairo-server-side-gradients.patch
        cairo-webkit-html5-fix.patch
        cairo-color-glyphs.patch)
sha1sums=('c6f7b99986f93c9df78653c3e6a3b5043f65145e'
          'b0cc2466cc5479f055ca2148cfa37fe13a1e78a6'
          'd8ffcb4c4745f7e61671109362a80a872ac989d3'
          '72ecf2dda8462e1588512de257ccbe18642d507f'
          '5bff494f52a16114f4cf6d04bfb0b9d7c4e9da23'
          'c25463ce365627ba55a2614d4542e17ca6ac7d51')

prepare(){
  cd $_basename-$pkgver

  patch -Np1 -i "${srcdir}"/cairo-make-lcdfilter-default.patch
  patch -Np1 -i "${srcdir}"/cairo-respect-fontconfig_pb.patch
  patch -Np1 -i "${srcdir}"/cairo-server-side-gradients.patch
  patch -Np1 -i "${srcdir}"/cairo-webkit-html5-fix.patch
  patch -Np1 -i "${srcdir}"/cairo-color-glyphs.patch
}

build() {
  cd $_basename-$pkgver

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --disable-lto \
    --enable-tee \
    --enable-gl \
    --enable-egl \
    --enable-svg \
    --enable-ps \
    --enable-pdf \
    --enable-gobject \
    --enable-gtk-doc

  make
}

package() {
  cd $_basename-$pkgver
  make DESTDIR="$pkgdir" install
}
