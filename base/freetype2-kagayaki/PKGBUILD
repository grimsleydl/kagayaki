# Maintainer: Julien Humbert <julroy67@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=freetype2-kagayaki
pkgname=('freetype2-kagayaki' 'freetype2-demos-kagayaki')
pkgver=2.8
pkgrel=1
pkgdesc="Font rasterization library"
arch=('i686' 'x86_64')
license=('GPL' 'MIT')
url="http://www.freetype.org"
depends=('zlib' 'bzip2' 'sh' 'libpng' 'harfbuzz')
makedepends=('libx11')
source=("https://download-mirror.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.bz2"
        "https://download-mirror.savannah.gnu.org/releases/freetype/freetype-doc-${pkgver}.tar.bz2"
        "https://download-mirror.savannah.gnu.org/releases/freetype/ft2demos-${pkgver}.tar.bz2"
        0001-Enable-table-validation-modules.patch
        0002-Enable-subpixel-rendering.patch
        0003-Enable-infinality-subpixel-hinting.patch
		0004-Enable-long-PCF-family-names.patch
        0005-freetype-2.5.2-more-demos.patch
        freetype2.sh
		xft-settings.sh)
sha256sums=('a3c603ed84c3c2495f9c9331fe6bba3bb0ee65e06ec331e0a0fb52158291b40b'
            '427ba04d11f450df4bac4c95fec247be1b835ccdcf85d8b081f3f39d31811154'
            '2b6ce0d36bcb43fcc8aac07a0287982d855571ee271c3803c768e501f9c1a233'
            '515d52643fa47bb96c99792c81d4c05694b4e08494d36c5f81f6d05b61d4f287'
            '40cf3b6e5390019a73c73d35fdf56a2b2bf380aefdb8c81ceae5dc94695f87e9'
            '7c99c299b6e46159e0be8179976736481358975bbe650d8c33ae5089c0da73f9'
            '7f5aa51a67a68002226f8e869eaa3f6b870c04d19135a14c64a6c355eb023a39'
            '36484db4b926ed026e7f32570573493b5a9793a129f08d54383a26d65a6af89b'
            'f7f8e09c44f7552c883846e9a6a1efc50377c4932234e74adc4a8ff750606467'
            '990d9169da0d8299df42e80894a5f2d32e3d83030ca48ca262ea19cadb8653d4')

prepare() {
	# Rename source dir to allow building the demos
	mv freetype-${pkgver} freetype2
	mv ft2demos-${pkgver} freetype2-demos
	
	cd freetype2
	patch -Np1 -i ../0001-Enable-table-validation-modules.patch
	patch -Np1 -i ../0002-Enable-subpixel-rendering.patch
	patch -Np1 -i ../0003-Enable-infinality-subpixel-hinting.patch
	patch -Np1 -i ../0004-Enable-long-PCF-family-names.patch
	
	cd ../freetype2-demos
	# enable more demos
	patch -Np1 -i ../0005-freetype-2.5.2-more-demos.patch
	
	# Suppress RPATH
	sed -i '/X11_LIB:%=-R%/d' graph/x11/rules.mk
}

build() {
	cd freetype2
	./configure \
		--prefix=/usr \
		--disable-static
	make
	
	# Build demos
	cd ../freetype2-demos
	make
}

check() {
	cd freetype2
	make -k check
}

package_freetype2-kagayaki() {
	provides=("freetype2=$pkgver" 'freetype2-infinality-ultimate' 'libfreetype.so')
	conflicts=('freetype2' 'freetype2-infinality-ultimate')
	replaces=('freetype2' 'freetype2-infinality-ultimate')
	groups=('kagayaki-bundle')
	install=freetype2-kagayaki.install
	backup=('etc/profile.d/freetype2.sh')

	cd freetype2
	make DESTDIR="${pkgdir}" install
	install -Dm644 ../freetype2.sh "${pkgdir}/etc/profile.d/freetype2.sh"

	# Package docs
	install -dm755 "${pkgdir}/usr/share/doc"
	cp -a docs "${pkgdir}/usr/share/doc/${pkgname}"

	# freetype2 runtime settings
	install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d"
	install -m755 "${srcdir}/xft-settings.sh" \
		"${pkgdir}/etc/X11/xinit/xinitrc.d/xft-settings.sh"
}

package_freetype2-demos-kagayaki() {
	pkgdesc="Freetype tools and demos"
	depends=('freetype2' 'libx11')

	cd freetype2-demos
	install -d "${pkgdir}/usr/bin"
	for _i in bin/{f,t}t*; do
		libtool --mode=install install $_i "${pkgdir}/usr/bin"
	done
}
