# Maintainer: Julien Humbert <julroy67@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=fontconfig-kagayaki
_basename=fontconfig
pkgver=2.12.3
pkgrel=1
pkgdesc="A library for configuring and customizing font access"
arch=('i686' 'x86_64')
url="https://www.freedesktop.org/wiki/Software/fontconfig/"
license=('custom' 'MIT')
groups=('kagayaki-bundle')
depends=('expat' 'freetype2-kagayaki' 'kagayaki-fonts-base')
makedepends=('python' 'docbook2x' 'gperf' 'python-lxml')
provides=('fontconfig=$pkgver' 'fontconfig-infinality-ultimate')
conflicts=('fontconfig' 'fontconfig-infinality-ultimate')
install='fontconfig-kagayaki.install'
_commit=79058f4e911487275323e93146e1e93ad15afcd8
source=("git+https://anongit.freedesktop.org/git/fontconfig#commit=$_commit"
	kagayaki-conf.tar.xz
	local.conf
    fc-cache-kagayaki.hook
	0001-configure.patch
	0002-configure.ac.patch
	0003-Makefile.in.patch
	0004-Makefile.conf.d.patch
	0005-Makefile.am.in.patch)
sha256sums=('SKIP'
            'cca4d33c314ff3eb4103aec8a1f5ef3188c7b048786126a5ac2accfa2c117116'
            '7d52f95cb473a826433e170dfe0c465dd8ffc48083ae422b07e42e5b2192cb50'
            'a4453b748b011029cc2395d817b7650f50418ae4eebb53f6521d81c6873400dd'
            'f7ea52a89dba51e803907f2ce61ae1bd3c055a99662737839f67399209cfed3d'
            'ec92e8e07a23d43e643c32d35ef0f9e8711f6b31cca1e1d56def0b1edfce6e60'
            '0676efe16ffe6a032fe2e8be1405974d633220ddcf7ea38cf686e521b3d83735'
            '4d3cb2643c46ad7eadee388bd2921a3e73137951205f124bed5b6337d8974871'
            'e25483bfa492a56e55e730c7ce0b1f6d5d3f0176a7330e23f6b326553c593015')

# a nice page to test font matching:
# http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html
# http://getemoji.com/

prepare() {
	# copy kagayaki stuff
	cd "kagayaki-conf"
	cp -r conf.d.kagayaki "${srcdir}/${_basename}"/conf.d.kagayaki
	
	# prepare src
	cd "${srcdir}/${_basename}"
	
	./autogen.sh

	patch -Np1 -i ../0001-configure.patch
	patch -Np1 -i ../0002-configure.ac.patch
	patch -Np1 -i ../0003-Makefile.in.patch
	patch -Np1 -i ../0004-Makefile.conf.d.patch
	patch -Np1 -i ../0005-Makefile.am.in.patch
	
	aclocal
	libtoolize -f
	automake -afi
}

build() {
	cd "$_basename"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--with-templatedir=/etc/fonts/conf.avail \
		--with-templateinfdir=/etc/fonts/conf.avail.kagayaki \
		--with-xmldir=/etc/fonts \
		--localstatedir=/var \
		--disable-static \
		--with-default-fonts=/usr/share/fonts \
		--with-add-fonts=/usr/share/fonts/kagayaki
	make
}

check() {
	cd "$_basename"
	make -k check
}

package() {
	cd "$_basename"
	make DESTDIR="$pkgdir" install
	
	# Install license
	install -Dm644 "COPYING" \
		"$pkgdir/usr/share/licenses/$pkgname/COPYING"
	
	# Kagayaki local.conf
	install -D -m 644 "${srcdir}"/local.conf \
		"${pkgdir}/etc/fonts/local.conf"

	# Alpm hook
	install -D -m 644 "${srcdir}"/fc-cache-kagayaki.hook \
		"${pkgdir}/usr/share/libalpm/hooks/90-fc-cache-kagayaki.hook"
}
