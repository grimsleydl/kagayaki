# Maintainer: Julien Humbert <julroy67@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=fontconfig-kagayaki
_basename=fontconfig
pkgver=2.12.1
pkgrel=12
pkgdesc="A library for configuring and customizing font access"
arch=('i686' 'x86_64')
url='http://www.fontconfig.org/release/'
license=('custom' 'MIT')
groups=('kagayaki-bundle')
depends=('expat' 'freetype2-kagayaki' 'kagayaki-fonts-base')
makedepends=('python2' 'docbook2x')
provides=('fontconfig=$pkgver' 'fontconfig-infinality-ultimate')
conflicts=('fontconfig' 'fontconfig-infinality-ultimate')
install='fontconfig-kagayaki.install'
source=("http://www.fontconfig.org/release/${_basename}-${pkgver}.tar.bz2"
	kagayaki-conf.tar.xz
	local.conf
    fc-cache-kagayaki.hook
	0001-configure.patch
	0002-configure.ac.patch
	0003-Makefile.in.patch
	0004-Makefile.conf.d.patch
	0005-Makefile.am.in.patch)
sha256sums=('b449a3e10c47e1d1c7a6ec6e2016cca73d3bd68fbbd4f0ae5cc6b573f7d6c7f3'
            'cca4d33c314ff3eb4103aec8a1f5ef3188c7b048786126a5ac2accfa2c117116'
            '9879a6dc3919e1e3c55283df058df3bef6dcbb11c11d241b3d5dfc34b4552c8a'
            '2762e927cd78c56a7d568684d8bb3cc73e2f6c268e0ba70de2a959fa09d36728'
            'f7ea52a89dba51e803907f2ce61ae1bd3c055a99662737839f67399209cfed3d'
            'ec92e8e07a23d43e643c32d35ef0f9e8711f6b31cca1e1d56def0b1edfce6e60'
            '0676efe16ffe6a032fe2e8be1405974d633220ddcf7ea38cf686e521b3d83735'
            '4d3cb2643c46ad7eadee388bd2921a3e73137951205f124bed5b6337d8974871'
            'e25483bfa492a56e55e730c7ce0b1f6d5d3f0176a7330e23f6b326553c593015')

# a nice page to test font matching:
# http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html
# http://getemoji.com/

prepare() {
	# copy kagayaki stuff
	cd "kagayaki-conf"
	
	cp -r conf.d.kagayaki "${srcdir}/${_basename}-${pkgver}"/conf.d.kagayaki
	
	# prepare src
	cd "${srcdir}/${_basename}-${pkgver}"
	
	patch -Np1 -i ../0001-configure.patch
	patch -Np1 -i ../0002-configure.ac.patch
	patch -Np1 -i ../0003-Makefile.in.patch
	patch -Np1 -i ../0004-Makefile.conf.d.patch
	patch -Np1 -i ../0005-Makefile.am.in.patch
	
	aclocal
	libtoolize -f
	automake -afi
}

build() {
	cd "$_basename-$pkgver"
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--with-templatedir=/etc/fonts/conf.avail \
		--with-templateinfdir=/etc/fonts/conf.avail.kagayaki \
		--with-xmldir=/etc/fonts \
		--localstatedir=/var \
		--disable-static \
		--with-default-fonts=/usr/share/fonts \
		--with-add-fonts=/usr/share/fonts/kagayaki
	make
}

#check() {
#	cd $_basename-$pkgver
#	make -k check
#}

package() {
	cd "$_basename-$pkgver"
	make DESTDIR="$pkgdir" install
	
	# Install license
	install -Dm644 "COPYING" \
		"$pkgdir/usr/share/licenses/$pkgname/COPYING"
	
	# Kagayaki local.conf
	install -D -m 644 "${srcdir}"/local.conf \
		"${pkgdir}/etc/fonts/local.conf"

	# Alpm hook
	install -D -m 644 "${srcdir}"/fc-cache-kagayaki.hook \
		"${pkgdir}/usr/share/libalpm/hooks/90-fc-cache-kagayaki.hook"
}
