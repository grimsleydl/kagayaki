# Maintainer: Julien Humbert <julroy67@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=freetype2
pkgname=lib32-$_pkgbasename-kagayaki
pkgver=2.8
pkgrel=1
pkgdesc="TrueType font rendering library"
arch=('x86_64')
license=('GPL' 'MIT')
url="http://freetype.sourceforge.net"
groups=('kagayaki-bundle-multilib')
depends=('lib32-zlib' 'lib32-bzip2' 'lib32-libpng' 'lib32-harfbuzz' $_pkgbasename-kagayaki)
makedepends=('gcc-multilib' 'libx11')
conflicts=('lib32-freetype2' 'lib32-freetype2-infinality-ultimate')
provides=('lib32-freetype2' 'lib32-freetype2-infinality-ultimate')
source=("https://download-mirror.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.bz2"
		0001-Enable-table-validation-modules.patch
		0002-Enable-subpixel-rendering.patch
		0003-Enable-infinality-subpixel-hinting.patch
		0004-Enable-long-PCF-family-names.patch)
sha1sums=('42c6b1f733fe13a3eba135f5025b22cb68450f91'
          'adcaec92eba6b539482c77ee669b313e1825c6e6'
          '0b36ec198c713337e0e75e95aca148e3c305d604'
          '9ff85c71a5cfab685aab17fcd8394e70e1330814'
          '334f229875039794adeb574e27d365bb445fb314')

prepare() {
	# Rename source dir to allow building the demos
	mv freetype-${pkgver} freetype2
	
	cd freetype2
	patch -Np1 -i ../0001-Enable-table-validation-modules.patch
	patch -Np1 -i ../0002-Enable-subpixel-rendering.patch
	patch -Np1 -i ../0003-Enable-infinality-subpixel-hinting.patch
	patch -Np1 -i ../0004-Enable-long-PCF-family-names.patch
}

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
	
	cd freetype2
	./configure \
		--prefix=/usr \
		--libdir=/usr/lib32 \
		--disable-static
	make
}

check() {
	cd freetype2
	make -k check
}

package() {
	cd freetype2
	make DESTDIR="${pkgdir}" install
	
	rm -rf "${pkgdir}"/usr/{include,share,bin}
}
