# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=merriweather-sans-kagayaki
pkgver=1.006
pkgrel=1
pkgdesc="Merriweather Sans font family by Eben Sorkin"
arch=('any')
url="http://sorkintype.com"
license=('custom:OFL')
depends=('fontconfig')
conflicts=('ttf-merriweather-kagayaki')
groups=('kagayaki-bundle-fonts')
source=("https://fonts.google.com/download?family=Merriweather%20Sans"
        42-merriweather-sans.conf)
sha256sums=('af0aa1c23b5f41656d52fb97887f6b3deb27f09a0114ff6cf0b631a6eff29bb3'
            '97b32a042306b27a6c989242adf98559761799c768b9d79eb735d9bec12d7f8d')

package(){
	cd "${srcdir}"

	# Install license
	install -Dm644 "OFL.txt" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 *.ttf "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 42-merriweather-sans.conf \
		"${pkgdir}"/etc/fonts/conf.avail/42-merriweather-sans.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/42-merriweather-sans.conf .
}
