# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cantarell-kagayaki
_basename=cantarell-fonts
pkgver=0.024
_commit=4fc722f
pkgrel=1
pkgdesc="Cantarell font family"
arch=('any')
url="https://github.com/GNOME/cantarell-fonts"
license=('GPL2')
depends=('fontconfig')
provides=('cantarell-fonts')
conflicts=('cantarell-fonts' 'otf-cantarell-kagayaki')
groups=('kagayaki-bundle-fonts-extra')
source=("https://github.com/GNOME/cantarell-fonts/archive/$_commit.zip"
        42-cantarell.conf)
sha1sums=('c6cc8fe18c43bab159bb663f7e080ebef49b8988'
          'c2fc52ab73d8b543bdf0d5b178e9e1b0fbdf3503')

build() {
	cd "${_basename}"-*
	./autogen.sh --prefix=/usr \
		--with-fontdir=/usr/share/fonts/kagayaki/${pkgname} \
		--with-configdir=/etc/fonts/conf.avail
	make
}

package() {
	cd "${_basename}"-*
	
	make DESTDIR="${pkgdir}" install

	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 ../42-cantarell.conf \
		"${pkgdir}"/etc/fonts/conf.avail/42-cantarell.conf
	
	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/42-cantarell.conf .
}
