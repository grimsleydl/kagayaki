# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=luxi-kagayaki
pkgver=1.0.3
pkgrel=1
pkgdesc="X.org Luxi Truetype fonts"
arch=('any')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
provides=('font-bh-ttf')
conflicts=('ttf-luxi-kagayaki' 'font-bh-ttf')
groups=('kagayaki-bundle-fonts')
source=("https://www.x.org/releases/individual/font/font-bh-ttf-${pkgver}.tar.bz2"
        42-luxi.conf)
sha256sums=('1b4bea63271b4db0726b5b52c97994c3313b6023510349226908090501abd25f'
            'a324b2dc9249d9fc67c8a2b02dafe3ee79f5d16b26530f45e5e492bd06ba18a0')

package() {
	cd "${srcdir}/font-bh-ttf-${pkgver}"

	# Install license
	install -Dm644 "COPYING" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 *.ttf "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 42-luxi.conf \
		"${pkgdir}"/etc/fonts/conf.avail/42-luxi.conf
	
	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/42-luxi.conf .
}
