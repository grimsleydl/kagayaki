# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=ubuntu-font-kagayaki
pkgver=0.83
pkgrel=2
pkgdesc="Ubuntu font family. Part of infinality-bundle-fonts"
arch=('any')
url="http://font.ubuntu.com/"
license=('custom:Ubuntu Font Licence 1.0')
depends=('fontconfig')
conflicts=('ttf-ubuntu-font-kagayaki' 'ttf-ubuntu-font-family')
groups=('kagayaki-bundle-fonts')
source=("http://font.ubuntu.com/download/ubuntu-font-family-${pkgver}.zip"
        42-ubuntu.conf)
sha1sums=('fad7939b33af4ae9ce803b403373ff49bbf1233f'
          '71be1aef98ece8a77eba0ae4aa8cc08ac8198073')

package() {
	cd "${srcdir}/ubuntu-font-family-${pkgver}"

	# Install license
	install -Dm644 "LICENCE.txt" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 Ubuntu*.ttf "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -D -m644 42-ubuntu.conf \
		"${pkgdir}"/etc/fonts/conf.avail/42-ubuntu.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/42-ubuntu.conf .
}
