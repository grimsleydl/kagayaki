# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=otf-cantarell-kagayaki
_basename=cantarell-fonts
pkgver=0.0.25
pkgrel=2
pkgdesc="Cantarell font family"
arch=('any')
url="https://github.com/GNOME/cantarell-fonts"
license=('GPL2')
depends=('fontconfig')
provides=('cantarell-fonts')
conflicts=('cantarell-fonts' 'otf-cantarell-ib')
groups=('kagayaki-bundle-fonts-extra')
source=(https://download.gnome.org/sources/${_basename}/${pkgver:0:3}/${_basename}-${pkgver}.tar.xz
        40-cantarell.conf)
sha256sums=('14a228aa0b516dfc367b434a850f955a00c57fc549cbb05348e2b150196a737f'
            '26e2eaff8c5f930602be0b247aef7d67a74d7c0a30c16202c7f75bb51a770154')

build() {
	cd "${_basename}-${pkgver}"
	./configure --prefix=/usr \
		--with-fontdir=/usr/share/fonts/kagayaki/${pkgname} \
		--with-configdir=/etc/fonts/conf.avail
	make
}

package() {
	cd "${_basename}-${pkgver}"
	
	make DESTDIR="${pkgdir}" install

	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 ../40-cantarell.conf \
		"${pkgdir}"/etc/fonts/conf.avail/40-cantarell.conf
	
	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/40-cantarell.conf .
}
