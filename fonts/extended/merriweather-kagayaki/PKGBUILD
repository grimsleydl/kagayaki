# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=merriweather-kagayaki
pkgver=2.002
pkgrel=1
pkgdesc="Merriweather Serif font family by Eben Sorkin"
arch=('any')
url="http://sorkintype.com"
license=('custom:OFL')
depends=('fontconfig')
conflicts=('ttf-merriweather-kagayaki')
groups=('kagayaki-bundle-fonts')
source=("https://fonts.google.com/download?family=Merriweather"
        42-merriweather.conf)
sha256sums=('5eaf3ba2fe1af3a5db38679e7df946038301a1f2ce18d587d19e2e2f1307c6e8'
            '56d4ae30289b6807924a4a4ab462f7845d2a68660a23ce2328725170fbf6fd8d')

package(){
	cd "${srcdir}"

	# Install license
	install -Dm644 "OFL.txt" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 *.ttf "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 42-merriweather.conf \
		"${pkgdir}"/etc/fonts/conf.avail/42-merriweather.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/42-merriweather.conf .
}
