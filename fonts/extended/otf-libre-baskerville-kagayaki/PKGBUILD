# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=otf-libre-baskerville-kagayaki
pkgver=1.0
pkgrel=2
pkgdesc="Libre Baskerville font designed by Pablo Impallari"
arch=('any')
url="http://www.impallari.com/"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('ttf-libre-baskerville-ibx' 'otf-libre-baskerville-ibx')
groups=('kagayaki-bundle-fonts')
source=("http://www.impallari.com/media/uploads/prosources/update-86-source.zip"
        40-libre-baskerville.conf)
sha256sums=('67ee4b67d55a8b9a16f234125bb89bffb9842e2b74f2c0e8f8479dde11f288d4'
            '79b7f9a9d8afa68dddc116ad0a55f1fe6adf35468069e6058db35ea6beecfdd8')

package(){
	cd "${srcdir}/Libre Baskerville v1.0"

	# Install license
	install -Dm644 "OFL.txt" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 src/*.otf "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 40-libre-baskerville.conf \
		"${pkgdir}"/etc/fonts/conf.avail/40-libre-baskerville.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/40-libre-baskerville.conf .
}
