# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=archivo-black-kagayaki
pkgver=1.004
pkgrel=2
pkgdesc="Archivo from Omnibus-Type is a grotesque sans-serif typeface family derived from Chivo"
arch=('any')
url="http://omnibus-type.com/"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('otf-archivo-black-kagayaki')
groups=('kagayaki-bundle-fonts')
source=("http://omnibus-type.com/download/ArchivoBlack-for-Print.zip"
        42-archivo-black.conf)
sha256sums=('b2ac07977b5483da0f49cec1a672e87b2d8e2011ca4fcf85137d1093be5517fe'
            '15386a354e06dc211229d849b0b29d3ebd34d2029553e9d2cdc8510d36a4176e')

package(){
	cd "${srcdir}/ArchivoBlack-for-Print"

	# Install license
	install -Dm644 "OFL.txt" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 ArchivoBlack.otf \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 42-archivo-black.conf \
		"${pkgdir}"/etc/fonts/conf.avail/42-archivo-black.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/42-archivo-black.conf .
}
