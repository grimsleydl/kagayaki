# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=symbola-kagayaki
pkgver=9.17
pkgrel=1
pkgdesc="Symbola is a free symbol-type font"
arch=('any')
url="http://users.teilar.gr/~g1951d/"
license=('custom')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('ttf-symbola-kagayaki' 'ttf-symbola')
groups=('kagayaki-bundle-fonts')
source=("http://users.teilar.gr/~g1951d/Symbola.zip"
        COPYING
        42-symbola.conf)
sha256sums=('dcb4a8fd44a161f6531a00c4946b40824b17e1732205a422dc141e3f9f47c42b'
            '78691fbb21caea139fd4105ea840c100cc3ce739b7430211d99ab5b15b14642e'
            '501e956044151e9ab69f3f2a33bb842540d0fcf96a5e6a53de1db44b697f8593')

package(){
	cd "${srcdir}"

	# Install license
	install -Dm644 "COPYING" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 Symbola_hint.ttf \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"/Symbola.ttf

	# Install infinality conf
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 42-symbola.conf \
		"${pkgdir}"/etc/fonts/conf.avail/42-symbola.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/42-symbola.conf .
}
