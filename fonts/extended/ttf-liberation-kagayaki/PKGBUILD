# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Andreas Zwinkau
# Contributor: rabyte <rabyte__gmail>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: bohoomil <@zoho.com>

pkgname=ttf-liberation-kagayaki
pkgver=2.00.1
_narrowver=1.07.4
pkgrel=2
pkgdesc='Red Hats Liberation fonts'
arch=('any')
url="https://fedorahosted.org/liberation-fonts/"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
provides=('ttf-font' 'ttf-liberation')
conflicts=('ttf-liberation' 'ttf-liberation-ib')
groups=('kagayaki-bundle-fonts')
source=("https://fedorahosted.org/releases/l/i/liberation-fonts/liberation-fonts-ttf-$pkgver.tar.gz"
        "https://fedorahosted.org/releases/l/i/liberation-fonts/liberation-fonts-ttf-$_narrowver.tar.gz"
        40-liberation.conf)
sha256sums=('7890278a6cd17873c57d9cd785c2d230d9abdea837e96516019c5885dd271504'
            '61a7e2b6742a43c73e8762cdfeaf6dfcf9abdd2cfa0b099a9854d69bc4cfee5c'
            'e76fb3f253b3079d00bda584017f4f8a5c237e90bc8e987957c698a30a880994')
package() {
	cd "${srcdir}/liberation-fonts-ttf-${pkgver}"

	# Install license
	install -Dm644 "LICENSE" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 *.ttf \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	cd "../liberation-fonts-ttf-${_narrowver}"
	install -m644 *Narrow-*.ttf \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 40-liberation.conf \
		"${pkgdir}"/etc/fonts/conf.avail/40-liberation.conf
	
	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/40-liberation.conf .
}
