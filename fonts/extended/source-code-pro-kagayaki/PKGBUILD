# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=source-code-pro-kagayaki
pkgver=2.030ro+1.050it
_relver=2.030R-ro/1.050R-it
_tarver=${_relver//\//-}
pkgrel=1
pkgdesc="A set of OpenType fonts designed for user interfaces"
arch=('any')
url="http://adobe-fonts.github.io/source-code-pro/"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('otf-source-code-pro-kagayaki')
groups=('kagayaki-bundle-fonts')
source=("${_tarver}.tar.gz::https://github.com/adobe-fonts/source-code-pro/archive/${_relver}.tar.gz"
		42-source-code-pro.conf)
sha256sums=('a4e4dd59b8e0a436b934f0f612c2e91b5932910c6d1c3b7d1a5a9f389c86302b'
            'b4988173ea26ce89e9501799c8df237ae6eb6fd2ac647657861b5441f6143d49')

package(){
	cd "${srcdir}/source-code-pro-$_tarver"

	# Install license
	install -Dm644 "LICENSE.txt" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 OTF/*.otf "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 42-source-code-pro.conf \
		"${pkgdir}"/etc/fonts/conf.avail/42-source-code-pro.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/42-source-code-pro.conf .
}
