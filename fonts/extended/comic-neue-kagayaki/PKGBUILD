# Maintainer: julroy67 <julroy67@gmail.com>

pkgname=comic-neue-kagayaki
pkgver=2.3
pkgrel=2
pkgdesc="Comic Neue aspires to be the casual script choice for everyone including the typographically savvy"
arch=('any')
url="http://comicneue.com/"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('otf-comic-neue-kagayaki')
groups=('kagayaki-bundle-fonts')
source=("http://comicneue.com/comic-neue-${pkgver}.zip"
        42-comic-neue.conf)
sha256sums=('f3a7500eb477d03a865249dd3fa5ecff516d12d11d5d8494c8164fd4ee842599'
            '0e6cd311f806bc7f561c3541c909a237c8625018412df81c8d905f690d115cf8')

package() {
	cd "${srcdir}"

	# Install license
	install -Dm644 "SIL-License.txt" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 OTF/*.otf "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 42-comic-neue.conf \
		"${pkgdir}"/etc/fonts/conf.avail/42-comic-neue.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/42-comic-neue.conf .
}
