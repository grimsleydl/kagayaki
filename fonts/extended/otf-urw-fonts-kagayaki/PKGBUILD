# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Firmicus <francois.archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: bohoomil <@zoho.com>

pkgname=otf-urw-fonts-kagayaki
_pkgname=urw-core35-fonts
pkgver=20160712
pkgrel=1
pkgdesc="URW+ Postscript core35 fonts"
arch=('any')
url="http://git.ghostscript.com/?p=urw-core35-fonts.git"
license=('GPL2')
depends=('fontconfig')
conflicts=('gsfonts' 't1-urw-fonts-ib')
replaces=('gsfonts' 't1-urw-fonts-ib')
provides=('gsfonts' 't1-urw-fonts-ib')
groups=('kagayaki-bundle-fonts')
source=("git+git://git.ghostscript.com/${_pkgname}.git#commit=79bcdfb34fbce12b592cce389fa7a19da6b5b018"
        45-urw-fonts.conf
        90-non-tt-urw-fonts.conf)
sha1sums=('SKIP'
          '4b591ded354e5bbac4c2537232959f12684a1cdf'
          'd4c365495dbc23538334f2451f35470e0f07fab8')

package() {
	cd "${srcdir}/${_pkgname}"
	
	install -d "${pkgdir}"/usr/share/fonts/${pkgname}
	install -m644 *.otf "${pkgdir}"/usr/share/fonts/${pkgname}
	
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 ../45-urw-fonts.conf \
		"${pkgdir}"/etc/fonts/conf.avail/45-urw-fonts.conf
	install -m644 ../90-non-tt-urw-fonts.conf \
		"${pkgdir}"/etc/fonts/conf.avail/90-non-tt-urw-fonts.conf
	
	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/45-urw-fonts.conf .
	ln -s ../conf.avail/90-non-tt-urw-fonts.conf .
}
