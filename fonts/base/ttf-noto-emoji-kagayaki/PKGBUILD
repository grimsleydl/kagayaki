# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: bohoomil <@zoho.com>

pkgname=ttf-noto-emoji-kagayaki
_commit=561d08a
pkgver=1.33
pkgrel=2
pkgdesc="Google Noto emoji fonts"
arch=(any)
url="https://www.google.com/get/noto/"
license=('Apache')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('noto-fonts-emoji' 'ttf-noto-fonts-emoji-ib')
provides=('noto-fonts-emoji' 'ttf-noto-fonts-emoji-ib')
makedepends=('cairo' 'optipng' 'python2-fonttools' 'nototools' 'imagemagick' 'zopfli' 'python2-setuptools')
source=("https://github.com/googlei18n/noto-emoji/archive/$_commit.zip"
        45-noto-emoji.conf
        90-non-tt-noto-emoji.conf)
sha256sums=('efb0474ad3b63e7842ad016023bec4ab306993975b453b812eb097de6b75192f'
            '43c3a516b98ae5347d10c7a8761e9d84b910747bf1910189ef18d115becc2bef'
            'ec415f42ff3c729f5874999df0ed07c2bf38c64ea5b2da9268ab4a9baa287b75')

prepare() {
	cd noto-emoji-*
	
	find -name '*.py' | xargs sed -e 's|/usr/bin/env python|/usr/bin/env python2|' -i
	sed -e 's|python|python2|g' -e 's|ttx "$<"|ttx2 "$<"|g' -i Makefile
}

build() {
	cd noto-emoji-*
	make -j6
}

package() {
	cd noto-emoji-*

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 NotoColorEmoji.ttf fonts/NotoEmoji-Regular.ttf \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m644 45-noto-emoji.conf \
		"${pkgdir}"/etc/fonts/conf.avail/45-noto-emoji.conf
	install -m644 90-non-tt-noto-emoji.conf \
		"${pkgdir}"/etc/fonts/conf.avail/90-non-tt-noto-emoji.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/45-noto-emoji.conf .
	ln -s ../conf.avail/90-non-tt-noto-emoji.conf .
}
