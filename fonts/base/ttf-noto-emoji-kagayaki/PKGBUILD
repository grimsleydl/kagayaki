# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: bohoomil <@zoho.com>

pkgname=ttf-noto-emoji-kagayaki
_commit=ff93f20
pkgver=1.33
pkgrel=5
pkgdesc="Google Noto emoji fonts"
arch=('any')
url="https://www.google.com/get/noto/"
license=('Apache')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('noto-fonts-emoji' 'ttf-noto-fonts-emoji-ib')
makedepends=('cairo' 'optipng' 'python2-fonttools' 'nototools' 'imagemagick' 'zopfli' 'python2-setuptools')
source=("https://github.com/googlei18n/noto-emoji/archive/$_commit.zip"
        40-noto-emoji.conf)
sha256sums=('70245c9f6a7736ccf3de7b3840f2f10561b8d39baed0edece3c216736a929e54'
            '073342e1d22a4dd4743a7e4c22d14f06788644039088121ab7a7a30e1a057465')

prepare() {
	cd noto-emoji-*
	
	find -name '*.py' | xargs sed -e 's|/usr/bin/env python|/usr/bin/env python2|' -i
	sed -e 's|python|python2|g' -e 's|ttx "$<"|ttx2 "$<"|g' -i Makefile
}

build() {
	cd noto-emoji-*
	make -j6
}

package() {
	cd noto-emoji-*

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 NotoColorEmoji.ttf fonts/NotoEmoji-Regular.ttf \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m644 40-noto-emoji.conf \
		"${pkgdir}"/etc/fonts/conf.avail/40-noto-emoji.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/40-noto-emoji.conf .
}
