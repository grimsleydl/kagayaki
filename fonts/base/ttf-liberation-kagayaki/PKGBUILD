# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Andreas Zwinkau
# Contributor: rabyte <rabyte__gmail>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: bohoomil <@zoho.com>

pkgname=ttf-liberation-kagayaki
pkgver=2.00.1
_narrowver=1.07.4
pkgrel=1
pkgdesc='Red Hats Liberation fonts'
arch=('any')
url="https://fedorahosted.org/liberation-fonts/"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
provides=('ttf-font' 'ttf-liberation' 'ttf-liberation-ib')
replaces=('ttf-liberation' 'ttf-liberation-ib')
conflicts=('ttf-liberation' 'ttf-liberation-ib')
groups=('kagayaki-bundle-fonts')
source=("https://fedorahosted.org/releases/l/i/liberation-fonts/liberation-fonts-ttf-$pkgver.tar.gz"
        "https://fedorahosted.org/releases/l/i/liberation-fonts/liberation-fonts-ttf-$_narrowver.tar.gz"
        45-liberation.conf
        90-tt-liberation.conf)
sha256sums=('7890278a6cd17873c57d9cd785c2d230d9abdea837e96516019c5885dd271504'
            '61a7e2b6742a43c73e8762cdfeaf6dfcf9abdd2cfa0b099a9854d69bc4cfee5c'
            'e76fb3f253b3079d00bda584017f4f8a5c237e90bc8e987957c698a30a880994'
            'e691a1b8a4b780017386863e6837a5f49b7aee438f5d12c64ea0141c5bac4be3')
package() {
	cd liberation-fonts-ttf-"${pkgver}"
	
	install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
	install -Dm644 LICENSE \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING
	
	install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
	install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"
	
	cd ../liberation-fonts-ttf-"${_narrowver}"
	install -m644 *Narrow-*.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"
	
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 45-liberation.conf \
		"${pkgdir}"/etc/fonts/conf.avail/45-liberation.conf
	install -m644 90-tt-liberation.conf \
		"${pkgdir}"/etc/fonts/conf.avail/90-tt-liberation.conf
	
	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/45-liberation.conf .
	ln -s ../conf.avail/90-tt-liberation.conf .
}
