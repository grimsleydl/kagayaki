# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=roboto-kagayaki
pkgver=2.138
pkgrel=4
pkgdesc="Google's signature family of fonts"
arch=('any')
url="https://github.com/google/roboto"
license=('Apache')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('ttf-roboto-kagayaki' 'ttf-roboto')
provides=('ttf-roboto')
groups=('kagayaki-bundle-fonts')
source=("https://github.com/google/roboto/releases/download/v${pkgver}/roboto-android.zip"
        41-roboto.conf)
sha256sums=('c825453253f590cfe62557733e7173f9a421fff103b00f57d33c4ad28ae53baf'
            '338b2302803efa95a00a62300c863ab0d41eef28743e104de86f2a752a4879b0')

package(){
	cd "${srcdir}"

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 Roboto*.ttf \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -D -m644 41-roboto.conf \
		"${pkgdir}"/etc/fonts/conf.avail/41-roboto.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/41-roboto.conf .
}
