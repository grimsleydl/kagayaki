# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>
# Contributor: bohoomil <@zoho.com>

pkgname=dejavu-kagayaki
pkgver=2.37
pkgrel=1
pkgdesc="Font family based on the Bitstream Vera Fonts with a wider range of characters"
arch=('any')
url="http://dejavu-fonts.org"
license=('custom')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
provides=('ttf-font' 'ttf-dejavu')
conflicts=('ttf-dejavu-kagayaki' 'ttf-dejavu')
groups=('kagayaki-bundle-fonts')
source=("http://downloads.sourceforge.net/project/dejavu/dejavu/${pkgver}/dejavu-fonts-ttf-${pkgver}.tar.bz2"
        41-dejavu.conf)
sha256sums=('fa9ca4d13871dd122f61258a80d01751d603b4d3ee14095d65453b4e846e17d7'
            '8765daf76e75cf23a51c5160f4ff31df3e023138c71df5e8dee26481aad17576')

package() {
	cd "${srcdir}"/dejavu-fonts-ttf-"${pkgver}"

	# Install license
	install -Dm644 "LICENSE" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 ttf/*.ttf "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 41-dejavu.conf \
		"${pkgdir}"/etc/fonts/conf.avail/41-dejavu.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/41-dejavu.conf .
}
