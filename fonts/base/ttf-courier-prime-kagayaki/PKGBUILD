# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=ttf-courier-prime-kagayaki
pkgver=1.203
pkgrel=1
pkgdesc="A free, improved, classical monospaced typeface"
arch=('any')
url="http://quoteunquoteapps.com/courierprime/"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
provides=('ttf-courier-prime-ib' 'ttf-courier-prime')
replaces=('ttf-courier-prime-ib' 'ttf-courier-prime')
conflicts=('ttf-courier-prime-ib' 'ttf-courier-prime')
groups=('kagayaki-bundle-fonts')
source=(http://quoteunquoteapps.com/downloads/courier-prime.zip
        45-courier-prime.conf
        90-tt-courier-prime.conf)
sha256sums=('d5d4faf1bee0d1f52bab1103cbfdfb354976331c86f999c110c22a098cb12d73'
            'bf60ee516132a80f4293b3418e5741121581cd4dc9c3080f05720b61d0b7006d'
            '98ca88805d49df11c362b24467b40aeedc4520cb16f4896ded02236b2785e0d6')

package(){
	cd "Courier Prime"
	
	install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
	install -D -m644 LICENSE/OFL.txt \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING
	
	install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
	install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"
	
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -D -m644 45-courier-prime.conf \
		"${pkgdir}"/etc/fonts/conf.avail/45-courier-prime.conf
	install -D -m644 90-tt-courier-prime.conf \
		"${pkgdir}"/etc/fonts/conf.avail/90-tt-courier-prime.conf
	
	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/45-courier-prime.conf .
	ln -s ../conf.avail/90-tt-courier-prime.conf .
}
