# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=ttf-courier-prime-kagayaki
pkgver=1.203
pkgrel=2
pkgdesc="A free, improved, classical monospaced typeface"
arch=('any')
url="http://quoteunquoteapps.com/courierprime/"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
provides=('ttf-courier-prime-ib' 'ttf-courier-prime')
replaces=('ttf-courier-prime-ib' 'ttf-courier-prime')
conflicts=('ttf-courier-prime-ib' 'ttf-courier-prime')
groups=('kagayaki-bundle-fonts')
source=(https://julroy67.freeboxos.fr:13476/share/h3Y-xMyPcgF6drIY/Fonts/courier-prime.zip
        45-courier-prime.conf
        90-tt-courier-prime.conf)
sha256sums=('d5d4faf1bee0d1f52bab1103cbfdfb354976331c86f999c110c22a098cb12d73'
            'bf60ee516132a80f4293b3418e5741121581cd4dc9c3080f05720b61d0b7006d'
            '465d8c4fe8ec996e6be21283ad96ee1c21503c02f576a414f66dbb62487634e4')

package(){
	cd "${srcdir}/Courier Prime"

	# Install license
	install -Dm644 "LICENSE/OFL.txt" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 *.ttf \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	
	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -D -m644 45-courier-prime.conf \
		"${pkgdir}"/etc/fonts/conf.avail/45-courier-prime.conf
	install -D -m644 90-tt-courier-prime.conf \
		"${pkgdir}"/etc/fonts/conf.avail/90-tt-courier-prime.conf
	
	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/45-courier-prime.conf .
	ln -s ../conf.avail/90-tt-courier-prime.conf .
}
