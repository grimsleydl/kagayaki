# Maintainer: julroy67 <julroy67@gmail.com>

pkgname=source-sans-pro-kagayaki
_pkgver=2019-01-12
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc="Sans serif font family for user interface environments"
arch=('any')
url="http://adobe-fonts.github.io/source-sans-pro"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
groups=('kagayaki-bundle-fonts')
source=("https://github.com/adobe-fonts/source-sans-pro/releases/download/2.045R-ro%2F1.095R-it/source-sans-pro-2.045R-ro-1.095R-it.zip"
		42-source-sans-pro.conf)
sha256sums=('5f090b821e117f452ef399c3c1b8b244c1647728a1e901c4d167cbf7fc298ab6'
            'ff03ede040aa38d3d5365eb7eefa666ba2bc1e041e168a4d7377a98a349df39d')

package(){
	cd "${srcdir}/source-sans-pro-2.045R-ro-1.095R-it"

	# Install license
	install -Dm644 "LICENSE.txt" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 OTF/*.otf "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 42-source-sans-pro.conf \
		"${pkgdir}"/etc/fonts/conf.avail/42-source-sans-pro.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/42-source-sans-pro.conf .
}
