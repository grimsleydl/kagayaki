# Maintainer: julroy67 <julroy67@gmail.com>

pkgname=source-sans-pro-kagayaki
_pkgver=2018-10-24
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc="Sans serif font family for user interface environments"
arch=('any')
url="http://adobe-fonts.github.io/source-sans-pro"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
groups=('kagayaki-bundle-fonts')
source=("https://github.com/adobe-fonts/source-sans-pro/releases/download/2.040R-ro%2F1.090R-it/source-sans-pro-2.040R-ro-1.090R-it.zip"
		42-source-sans-pro.conf)
sha256sums=('dec6fa04d142762a9051dc36be356291a9cf5864466539e2712879370d88818c'
            'ff03ede040aa38d3d5365eb7eefa666ba2bc1e041e168a4d7377a98a349df39d')

package(){
	cd "${srcdir}/source-sans-pro-release"

	# Install license
	install -Dm644 "LICENSE.txt" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 OTF/*.otf "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 42-source-sans-pro.conf \
		"${pkgdir}"/etc/fonts/conf.avail/42-source-sans-pro.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/42-source-sans-pro.conf .
}
