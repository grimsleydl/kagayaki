# Maintainer: julroy67 <julroy67@gmail.com>

pkgname=source-sans-pro-kagayaki
_pkgver=2017-08-15
_commit=ff3bdec0a9ed367bfd6b05572f5dabebf030e7bb
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc="Sans serif font family for user interface environments"
arch=('any')
url="http://adobe-fonts.github.io/source-sans-pro"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
makedepends=('afdko')
groups=('kagayaki-bundle-fonts')
source=("https://github.com/adobe-fonts/source-sans-pro/archive/${_commit}.zip"
		42-source-sans-pro.conf)
sha256sums=('261b94c1877556913c0071bacc1e59a9a018d5fc1235490b703a5489f45412b4'
            'ff03ede040aa38d3d5365eb7eefa666ba2bc1e041e168a4d7377a98a349df39d')

build(){
	cd "${srcdir}/source-sans-pro-${_commit}"

	sh build.sh
}

package(){
	cd "${srcdir}/source-sans-pro-${_commit}"

	# Install license
	install -Dm644 "LICENSE.md" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 target/OTF/*.otf "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 42-source-sans-pro.conf \
		"${pkgdir}"/etc/fonts/conf.avail/42-source-sans-pro.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/42-source-sans-pro.conf .
}
