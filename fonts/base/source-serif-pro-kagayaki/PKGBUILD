# Maintainer: julroy67 <julroy67@gmail.com>

pkgname=source-serif-pro-kagayaki
_pkgver=2019-01-12
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc="Serif typeface for setting text. Designed to complement Source Sans Pro"
arch=('any')
url="http://adobe-fonts.github.io/source-serif-pro"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
groups=('kagayaki-bundle-fonts')
source=("https://github.com/adobe-fonts/source-serif-pro/releases/download/2.010R-ro%2F1.010R-it/source-serif-pro-2.010R-ro-1.010R-it.zip"
		42-source-serif-pro.conf)
sha256sums=('0458cd27718235356667d018d725585d1134a5e36f87408cce8d164b7be7764c'
            '0a235b05e6ece74e7c37c8c55278efcc1b5dfe6d9a189ecbe0cddbe714a219dc')

package(){
	cd "${srcdir}/source-serif-pro-2.010R-ro-1.010R-it"

	# Install license
	install -Dm644 "LICENSE.md" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 OTF/*.otf "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 42-source-serif-pro.conf \
		"${pkgdir}"/etc/fonts/conf.avail/42-source-serif-pro.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/42-source-serif-pro.conf .
}
