# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=erewhon-kagayaki
pkgver=1.08
pkgrel=2
pkgdesc="Erewhon is based on the Heuristica family, which is based in turn on Utopia"
arch=('any')
url="http://www.ctan.org/tex-archive/fonts/erewhon"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('otf-erewhon-kagayaki')
groups=('kagayaki-bundle-fonts')
source=("http://mirrors.ctan.org/fonts/erewhon.zip"
        41-erewhon.conf)
sha256sums=('d1821e2d1fcf16192fd2e32b29b09ae72bed810ea67b33c92d8f285cb4d75748'
            '84d3fcf09d7373c7c0836e5755d8313baba70969c1230c274e0e910cd8f817ac')

package(){
	cd "${srcdir}/erewhon"

	# Install license
	install -Dm644 doc/"OFL.txt" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 opentype/*.otf "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 41-erewhon.conf \
		"${pkgdir}"/etc/fonts/conf.avail/41-erewhon.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/41-erewhon.conf .
}
