# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=otf-tex-gyre-kagayaki
pkgver=2.005
pkgrel=2
pkgdesc="The TeX Gyre Collection of Fonts"
arch=('any')
url="http://www.gust.org.pl/projects/e-foundry/tex-gyre/"
license=('GFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
provides=('otf-tex-gyre-ib' 'otf-texgyre' 'gsfonts')
replaces=('otf-tex-gyre-ib' 'otf-texgyre' 'gsfonts')
conflicts=('otf-tex-gyre-ib' 'otf-texgyre' 'gsfonts')
groups=('kagayaki-bundle-fonts')
source=("http://www.gust.org.pl/projects/e-foundry/tex-gyre/whole/tg-${pkgver}otf.zip"
        GFL.txt
        45-tex-gyre.conf
        90-tt-tex-gyre.conf)
sha256sums=('6158bdb106e5b017743cb6e9ddb6ad4c7153e82fafadd6eb5d62c9f3064df04e'
            '5752b69cd0a5c1ed27242d672ce3031ac9ed4e3a1ba2265db3676cf2689f67c1'
            'e0a49b011eac2e51dbb844706825c3b84f4c9a0a1640d2d6f8b462dd29bb2fe7'
            '26d425267afd1977abef37f83948206d5c05db7cc0c2505cefaaf261f508eee3')

package(){
	cd "${srcdir}"

	# Install license
	install -Dm644 "GFL.txt" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 *.otf \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 45-tex-gyre.conf \
		"${pkgdir}"/etc/fonts/conf.avail/45-tex-gyre.conf
	install -m644 90-tt-tex-gyre.conf \
		"${pkgdir}"/etc/fonts/conf.avail/90-non-tt-tex-gyre.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/45-tex-gyre.conf .
	ln -s ../conf.avail/90-tt-tex-gyre.conf .
}
