# Maintainer: julroy67 <julroy67@gmail.com>

pkgname=source-code-pro-kagayaki
_pkgver=2017-10-13
_commit=a1377187cae5c15e4db5569b57d25aede5f4a1cd
pkgver=${_pkgver//-}
pkgrel=1
epoch=1
pkgdesc="Monospaced font family for user interface and coding environments"
arch=('any')
url="http://adobe-fonts.github.io/source-code-pro/"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('otf-source-code-pro-kagayaki')
makedepends=('afdko')
groups=('kagayaki-bundle-fonts')
source=("https://github.com/adobe-fonts/source-code-pro/archive/${_commit}.zip"
		41-source-code-pro.conf)
sha256sums=('c51aab030e85374a684a7133d3873df39d82b129c6ea8ae273682084a11eadb5'
            '5ddb03ff86496081ac5311b217789d9cb1ec738d9610c4c363a15835e75f512d')

build(){
	cd "${srcdir}/source-code-pro-${_commit}"

	sh build.sh
}

package(){
	cd "${srcdir}/source-code-pro-${_commit}"

	# Install license
	install -Dm644 "LICENSE.md" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 target/OTF/*.otf "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m644 41-source-code-pro.conf \
		"${pkgdir}"/etc/fonts/conf.avail/41-source-code-pro.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/41-source-code-pro.conf .
}
