# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: bohoomil <@zoho.com>

pkgname=noto-emoji-kagayaki
pkgver=2.011
_commit=b73feb8bc5b4a98c37883e700d6deb58a497de62
pkgrel=1
pkgdesc="Google Noto emoji fonts"
arch=('any')
url="https://www.google.com/get/noto/"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('ttf-noto-emoji-kagayaki' 'noto-fonts-emoji')
source=("https://github.com/googlei18n/noto-emoji/archive/$_commit.zip"
        41-noto-emoji.conf)
sha256sums=('9f7cc22b87be6e8c0ed2c55b3d0f4134ce1b5c1398823b367060ca5a33581ea3'
            'a7885d9bf967dc4c18ec8818d791365f1682ff03732d2dccc19dc8b907fe6543')

package() {
	cd "noto-emoji-${_commit}/fonts"

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 NotoColorEmoji.ttf \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	
	# License
	install -Dm644 "LICENSE" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m644 41-noto-emoji.conf \
		"${pkgdir}"/etc/fonts/conf.avail/41-noto-emoji.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/41-noto-emoji.conf .
}
