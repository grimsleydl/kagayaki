# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=noto-fonts-cjk-kagayaki
pkgver=1.004
pkgrel=2
pkgdesc="Google Noto TTF CJK fonts"
arch=('any')
url="https://www.google.com/get/noto/"
license=('custom:SIL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('ttf-noto-fonts-cjk-kagayaki')
groups=('kagayaki-bundle-fonts')
source=("https://noto-website.storage.googleapis.com/pkgs/NotoSansCJK.ttc.zip"
		"https://noto-website.storage.googleapis.com/pkgs/NotoSerifCJK.ttc.zip"
		43-noto-cjk.conf)
sha256sums=('52dd6f2780b8e5cc137a0fcaaf45a0143a4261b4b07c165ae4cd1b7878b148ab'
            '87e29a16ddd16c6b4bb83ae8cbd843ca6f757d12eae06f3462149534ce99f284'
            '04a0a0270bd7e8bf44f96f1c6079afb01f7e87eec2de8f954b9961705a39af78')

package() {
	cd "${srcdir}"

	bsdtar -xf NotoSansCJK.ttc.zip
	bsdtar -xf NotoSerifCJK.ttc.zip

	# Install license
	install -Dm644 "LICENSE_OFL.txt" \
		"${pkgdir}"/usr/share/licenses/"$pkgname"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 NotoSansCJK.ttc \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 NotoSerifCJK.ttc \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m644 43-noto-cjk.conf \
		"${pkgdir}"/etc/fonts/conf.avail/43-noto-cjk.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/43-noto-cjk.conf .
}
