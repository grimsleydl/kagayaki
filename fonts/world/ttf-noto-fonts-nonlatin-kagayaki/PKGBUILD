# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=ttf-noto-fonts-nonlatin-kagayaki
_pkgver=2017-05-05
_commit=28c49d2
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc="Google Noto TTF fonts: non-Latin extension"
arch=('any')
url="http://www.google.com/get/noto/"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('ttf-noto-fonts-nonlatin-ib')
groups=('kagayaki-bundle-fonts')
source=("https://github.com/googlei18n/noto-fonts-alpha/archive/$_commit.zip"
        40-noto-nonlatin.conf)
sha256sums=('bb4789bfeb9b0e6ca7519a898a263f5c521806552144d115e5e1c545f464da8a'
            '11cd3fe038c2e6ddb95120067f01dd0764b4ecb9672ed203f2f1b700c8b8c46d')

package() {
	cd "${srcdir}"/noto-fonts-alpha-*
	cd "from-pipeline/unhinted/variable-ttf/sans"

	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	_nonlatinsansalpha=(NotoSansChakma-Regular-VF.ttf NotoSansMath-Regular-VF.ttf NotoSansOsage-Regular-VF.ttf NotoSansSymbols2-Regular-VF.ttf)

	for fnt in "${_nonlatinsansalpha[@]}"; do
		install -m644 "${fnt}" "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	done

	cd "../serif"

	_nonlatinserifalpha=(NotoSerifEthiopic-VF.ttf NotoSerifMyanmar-VF.ttf NotoSerifSinhala-VF.ttf)

	for fnt in "${_nonlatinserifalpha[@]}"; do
		install -m644 "${fnt}" "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	done

	# License
	cd "${srcdir}"/noto-fonts-alpha-*
	install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
	install -m644 "LICENSE" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# fontconfig templates
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m644 40-noto-nonlatin.conf \
		"${pkgdir}"/etc/fonts/conf.avail/40-noto-nonlatin.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/40-noto-nonlatin.conf .
}
