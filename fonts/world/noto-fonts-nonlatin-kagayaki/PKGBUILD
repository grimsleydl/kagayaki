# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=noto-fonts-nonlatin-kagayaki
_pkgver=2017-08-02
_commit=93ed31f
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc="Google Noto TTF fonts: non-Latin extension"
arch=('any')
url="http://www.google.com/get/noto/"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('ttf-noto-fonts-nonlatin-kagayaki')
groups=('kagayaki-bundle-fonts')
source=("https://github.com/googlei18n/noto-fonts-alpha/archive/$_commit.zip"
        43-noto-nonlatin.conf)
sha256sums=('79895de64b2e8bf662dc6c65318fb80d598adb08301b1e76f6cebc2387b1f392'
            '11cd3fe038c2e6ddb95120067f01dd0764b4ecb9672ed203f2f1b700c8b8c46d')

package() {
	cd "${srcdir}"/noto-fonts-alpha-*
	cd "from-pipeline/unhinted/variable-ttf/sans"

	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	_nonlatinsansalpha=(NotoSansChakma-Regular-VF.ttf NotoSansMath-Regular-VF.ttf NotoSansOsage-Regular-VF.ttf NotoSansSymbols2-Regular-VF.ttf)

	for fnt in "${_nonlatinsansalpha[@]}"; do
		install -m644 "${fnt}" "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	done

	cd "../serif"

	_nonlatinserifalpha=(NotoSerifEthiopic-VF.ttf NotoSerifMyanmar-VF.ttf NotoSerifSinhala-VF.ttf)

	for fnt in "${_nonlatinserifalpha[@]}"; do
		install -m644 "${fnt}" "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	done

	# License
	cd "${srcdir}"/noto-fonts-alpha-*
	install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
	install -m644 "LICENSE" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# fontconfig templates
	cd "${srcdir}"
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m644 43-noto-nonlatin.conf \
		"${pkgdir}"/etc/fonts/conf.avail/43-noto-nonlatin.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/43-noto-nonlatin.conf .
}
