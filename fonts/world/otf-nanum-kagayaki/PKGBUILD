# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: darehanl <darehanl@150mail.com>

pkgname=otf-nanum-kagayaki
pkgver=3.1
pkgrel=3
pkgdesc="The Nanum fonts are unicode fonts designed especially for the Korean-language script"
arch=('any')
url="http://hangeul.naver.com/"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
makedepends=('p7zip')
conflicts=('ttf-nanum' 'ttf-nanum-fonts-ibx')
groups=('kagayaki-bundle-fonts')
_filename="NanumFontSetup_OTF_ALL_hangeulcamp.exe"
source=("http://appdown.naver.com/naver/font/NanumFont/setup/${_filename}"
		40-nanum.conf
		OFL.txt)
sha256sums=('f4196e2c305c46bc0d089dfad337262aecb34f0b1b2311022fb578356aef79c0'
            '4ac02f36514e8784d9ca0384f62f014f429083f839dbb37cf26d1e163d9ba1ee'
            '274ab590e1297b4cdeacc3a0d896468ee3dd5827b6f84c46bf86eed46610f13f')

prepare() {
	cd "${srcdir}"
	7z x -y "${_filename}" "\$WINDIR/Fonts"
}

package() {
	cd "${srcdir}"

	# Install license
	install -Dm644 "OFL.txt" \
		"${pkgdir}"/usr/share/licenses/"$pkgname"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 \$WINDIR/Fonts/NanumGothic*.otf \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 \$WINDIR/Fonts/NanumMyeongjo*.otf \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m644 40-nanum.conf \
		"${pkgdir}"/etc/fonts/conf.avail/40-nanum.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/40-nanum.conf .
}
