# Maintainer: julroy67 <julroy67@gmail.com>
# Contributor: darehanl <darehanl@150mail.com>

pkgname=otf-nanum-kagayaki
pkgver=3.1
pkgrel=2
pkgdesc="The Nanum fonts are unicode fonts designed especially for the Korean-language script"
arch=('any')
url="http://hangeul.naver.com/"
license=('custom:OFL')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
makedepends=('p7zip')
provides=('ttf-nanum' 'ttf-nanum-fonts-ibx')
conflicts=('ttf-nanum' 'ttf-nanum-fonts-ibx')
replaces=('ttf-nanum' 'ttf-nanum-fonts-ibx')
groups=('kagayaki-bundle-fonts')
_filename="NanumFontSetup_OTF_ALL_hangeulcamp.exe"
source=("http://appdown.naver.com/naver/font/NanumFont/setup/${_filename}"
		40-nanum.conf
		90-tt-nanum.conf)
sha256sums=('f4196e2c305c46bc0d089dfad337262aecb34f0b1b2311022fb578356aef79c0'
            '42b260f5937c226b7c6d6b2aab9e868ff386a60914371a7bd2d6f83b84b93da8'
            '3d601e4753b3029c801a6113a60f90ae80aa501b2e862e0c37876c0e85448215')

prepare() {
	cd "${srcdir}"
	7z x -y "${_filename}" "\$WINDIR/Fonts"
}

package() {
	cd "${srcdir}"

	# Install license
	install -Dm644 "OFL.txt" \
		"${pkgdir}"/usr/share/licenses/"$pkgname"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 \$WINDIR/Fonts/*.otf \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"

	# Install infinality conf
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m644 40-nanum.conf \
		"${pkgdir}"/etc/fonts/conf.avail/40-nanum.conf
	install -m644 90-tt-nanum.conf \
		"${pkgdir}"/etc/fonts/conf.avail/90-tt-nanum.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/40-nanum.conf .
	ln -s ../conf.avail/90-tt-nanum.conf .
}
