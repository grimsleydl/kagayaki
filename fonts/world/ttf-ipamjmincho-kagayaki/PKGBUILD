# Maintainer: Julien Humbert <julroy67@gmail.com>
# Contributor: bohoomil <@zoho.com>

pkgname=ttf-ipamjmincho-kagayaki
pkgver=004.01
_pkgver=${pkgver/./}
pkgrel=4
pkgdesc="Japanese outline Mincho fonts including the unified characters of the Basic Resident Registers Network and the Family Register of Japan (based on IPAex Mincho fonts)"
arch=('any')
url="http://mojikiban.ipa.go.jp/1300.html"
license=('custom:IPA')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
conflicts=('ttf-ipamjfont-ibx')
groups=('kagayaki-bundle-fonts')
source=("http://dl.mojikiban.ipa.go.jp/IPAmjMincho/ipamjm${_pkgver}.zip"
	40-ipamjmincho.conf)
sha256sums=('eab2078bfb92ca38d2e06bfd0054e0f841062ac67804436f105d354ccef07944'
            '7559f1f9761435ecc196958b096ea4a8c338e868b3a4f92e91e05255182e3dcc')

package() {
	cd "${srcdir}"

	# Install license
	install -Dm644 "IPA_Font_License_Agreement_v1.0.txt" \
		"${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

	# Install font
	install -m755 -d "${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	install -m644 ipamjm.ttf \
		"${pkgdir}"/usr/share/fonts/kagayaki/"${pkgname}"
	
	# Install infinality conf
	install -m755 -d "${pkgdir}"/etc/fonts/conf.d
	install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
	install -m644 40-ipamjmincho.conf \
		"${pkgdir}"/etc/fonts/conf.avail/40-ipamjmincho.conf

	cd "${pkgdir}"/etc/fonts/conf.d
	ln -s ../conf.avail/40-ipamjmincho.conf .
}
